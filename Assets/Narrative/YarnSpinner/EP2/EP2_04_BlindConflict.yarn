title: EP2_04_BlindConflict
tags:
position: -324,135
---
<<declare $AshOuted = false>>
<<declare $BlindsClosed = false>>
<<TeleportToLocation NPCManager Ash DiscussionRoom 5>>
<<ToggleBlur false>>
<<wait 1>>

<<ToggleBlur true>>
<<PlayAnimation GlobalAnimationController Ash 28>>
Ash: Umm... a bit too bright in here. Sorry, let me just close the blinds.
<<PlayAnimation GlobalAnimationController Ash 2>>
<<ToggleBlur false>>

<<CameraFocusOnNPC GameManager Ash true>>
<<ToggleConversationUI false>>
<<MoveToLocation Ash DiscussionRoom 4>>
<<wait 3.5>>
<<NPCToggleBlinds ActionBlinds true>>
<<wait 1>>
<<ToggleConversationUI true>>
<<CameraFocusOnNPC GameManager Ash false>>

<<ToggleBlur true>>
<<PlayAnimation GlobalAnimationController Ash 13>>
Ash: Much better.
<<PlayAnimation GlobalAnimationController Ash 2>>
<<ToggleBlur false>>

<<MoveToLocation Ash DiscussionRoom 0>>
<<wait 0.5>>
<<TeleportToLocation NPCManager Ming DiscussionRoom 5>>
<<wait 1>>
<<TurnToNPC Ash Player 1.0 true true>>

<<ToggleBlur true>>
<<PlayAnimation GlobalAnimationController Ming 13>>
Ming: Folks, the weather outside is perfect. Bright and sunny!

<<PlayAnimation GlobalAnimationController Ming 4>>
Ming: Let's keep those blinds open.
<<PlayAnimation GlobalAnimationController Ming 2>>
<<ToggleBlur false>>


<<CameraFocusOnNPC GameManager Ming true>>
<<ToggleConversationUI false>>
<<MoveToLocation Ming DiscussionRoom 4>>
<<wait 3.5>>
<<NPCToggleBlinds ActionBlinds false>>
<<wait 1>>
<<ToggleConversationUI true>>
<<ToggleBlur true>>
<<CameraFocusOnNPC GameManager Ming false>>
<<TurnToNPC Ming Player 1.0 true true>>

<<MoveToLocation Ming DiscussionRoom 3>>

-> Support Ash by adjusting the blinds.
    <<jump CloseBlinds>>
-> Keep silent.
    <<jump DoNothing>>
-> Make Ming aware of Ash's sensitivity and then adjust the blinds.
    <<jump OutAsh>>
===
title: CloseBlinds
position: 120,-51
---
<<set $AshOuted = false>>
<<set $BlindsClosed = true>>
<<RemoveNextUltimatePoint TaskManager 19>>
<<ToggleBlur true>>

<<PlayAnimation GlobalAnimationController Player 13>>
You: It's definitely a nice day, but...

-> Worried the glare might mess with the TV's clarity.
    <<PlayAnimation GlobalAnimationController Player 4>>
    You: I am worried that the glare might mess with the TV's clarity.
    You: How about we save the view for after the meeting? 
    <<ToggleFullClose ActionBlinds 2>>
-> How about we find a balance with the natural light?
    <<PlayAnimation GlobalAnimationController Player 4>>
    You: We can close the blinds halfway, that way the room stays bright and fresh. 
    <<ToggleFullClose ActionBlinds 1>>
-> Let's focus now and enjoy later.
    <<PlayAnimation GlobalAnimationController Player 4>>   
    You: How about closing the blinds for most of the meeting and opening them towards the end?
    <<ToggleFullClose ActionBlinds 2>>


<<SetTakeawayCardState 4_respect_privacy 4.1.CloseBlinds>>
<<PlayAnimation GlobalAnimationController Player 2>>


<<PlayAnimation GlobalAnimationController Ming 13>>
Ming: Yeah sure! That sounds fair actually.
<<PlayAnimation GlobalAnimationController Ming 2>>

<<PlayAnimation GlobalAnimationController Ash 13>>
Ash: Yeah, we can do that.
<<PlayAnimation GlobalAnimationController Ash 2>>
<<ToggleBlur false>>


===
title: DoNothing
position: 144,144
---
<<set $AshOuted = false>>
<<set $BlindsClosed = false>>
<<ToggleFullClose ActionBlinds 0>>
<<RemoveNextUltimatePoint TaskManager 18>> 
<<SetTakeawayCardState 4_respect_privacy 4.2.DoNothing>>
<<ToggleBlur true>>


<<PlayAnimation GlobalAnimationController Ash 12>>
Ash: I get that, but I'm kind of like a gremlin – bright light is not my friend. 

<<PlayAnimation GlobalAnimationController Ming 4>>
Ming: A gremlin, huh? Well, I don’t know, Ash. I really think the light is good for us. Maybe it's just a matter of getting used to it?
<<PlayAnimation GlobalAnimationController Ming 2>>


<<PlayAnimation GlobalAnimationController Ash 19>>
Ash: ...
<<PlayAnimation GlobalAnimationController Ash 2>>
<<ToggleBlur false>>


===
title: OutAsh
position: 109,394
---

<<set $AshOuted = true>>
<<set $BlindsClosed = true>>
<<ToggleFullClose ActionBlinds 2>>
<<RemoveNextUltimatePoint TaskManager 19>>
<<ToggleBlur true>>


<<PlayAnimation GlobalAnimationController Player 13>>
You: Oh actually, Ash is autistic and sensitive to bright light.

<<SetTakeawayCardState 4_respect_privacy 4.3.RespondForAsh>>
You: That’s why she closed it earlier.

<<PlayAnimation GlobalAnimationController Player 13>>
You: Let me close it for you, Ash.
<<PlayAnimation GlobalAnimationController Player 2>>

<<PlayAnimation GlobalAnimationController Ash 25>>
Ash: Uh...
<<PlayAnimation GlobalAnimationController Ash 2>>

<<ToggleBlur false>>



===

